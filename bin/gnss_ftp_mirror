#!/usr/bin/perl 

=head1 gnss_ftp_mirror

Simple ftp mirror script to recursely mirror files from a remote ftp site 
to a local directory.  

The script is designed to interrogate a GNSS archive type file system with 
directories and files named after year, day, station code etc.  

The configuration file defines the structure of the file name and path in
terms of these components.  As the files are downloaded they can be renamed
using these fields.  

The script looks for a range of files based on their age in days (actually
the age of the data, not the file).  For example it can download files 
with dates matching the last 30 days.

=head2 Synopsis:

 gnss_ftp_mirror.pl [options] config_file [item=value ...]

The parameters are the name of a configuration, and a set of configuration
values which override the values in the configuration file.

Options are:

=over

=item  -v            

Verbose mode - lists information about downloads etc

=item  -V            

More verbose mode - additional information only messages

=item -s ###        

Specify the start date for downloads as days before the current date.  eg -s 30
or in a GNSS date format eg 2014-050.

=item -e ###        

Specify the end date for downloads as days before the current date.  eg -s 30
or in a GNSS date format eg 2014-050.

=item -p ##

Specifies a pause in seconds between successive downloads.  This is to be kind
to the server!  Default is 1 second unless defined otherwise in the configuration
file

=item -r ##

Specifies the maximum runtime for in minutes.  Downloads will be aborted after this 
time.

=item -i

Ignore file size. If present then all matching files are downloaded.  
Otherwise files will not be downloaded if there is already an existing file
of the same size.

=item -c

Print an example configuration file

=back

=cut

use strict;
use Getopt::Std;
use URI;
use Net::FTP;
use Cwd;
use File::Path;
use File::Copy;
use Config::General;
use POSIX qw/strftime/;
use LINZ::RunBatch;
use LINZ::GNSS::Time qw/parse_gnss_date seconds_yearday time_elements seconds_ymdhms $SECS_PER_DAY/;

my $syntax=<<EOD;

Syntax: gnss_ftp_mirror.pl [-b|-B logfile] [options] config_file [item=value ...]

Options are:
   -b            Run in batch mode starting with 'at now'
   -B logfile    Run in batch mode with a specified log file
   -v            Verbose mode
   -V            Even more verbose
   -s ###        Start date to download (days before now)
   -e ###        End date to download (days before now)
   -p ##         Reset pause between downloads
   -r ###        Maximum run time in minutes
   -i            Ignore file size - download all matching files
   -c            Print an example configuration file

EOD

#=====================================================================

my @months=('jan','feb','mar','apr','may','jun','jul','aug','sep','oct','nov','dec');
my @umonths=();
my @ccmonths=();
my @fullmonths=('january','february','march','april','may','june',
                'july','august','september','october','november','december');
my @ufmonths=();
my @cfmonths=();

foreach my $mon (@months) 
{
    push(@umonths,uc($mon));
    my $ccmon=$mon;
    substr($ccmon,0,1)=uc(substr($ccmon,0,1));
    push(@ccmonths,uc($mon));
}

foreach my $mon (@fullmonths) 
{
    push(@ufmonths,uc($mon));
    my $ccmon=$mon;
    substr($ccmon,0,1)=uc(substr($ccmon,0,1));
    push(@cfmonths,uc($mon));
}

my %patterns=(
    'yyyy' => '\d\d\d\d',
    'yy' =>   '\d\d',
    'mm' =>   '\d\d',
    'mmm' =>  '('.join('|',@months).')',
    'Mmm' =>  '('.join('|',@ccmonths).')',
    'MMM' =>  '('.join('|',@umonths).')',
    'dd'  =>  '\d\d',
    'ddd' =>  '\d\d\d',
    'code' => '[a-z0-9]{4}',
    'CODE' => '[A-Z0-9]{4}',
    'Code' => '[A-Za-z0-9]{4}',
);

#=================================================================
# Get options
my %opts;
getopts('Vvcis:e:p:r:',\%opts);

if( $opts{c} )
{
    printExampleConfig();
    exit();
}

my $verbose=$opts{V} ? 2 : $opts{v} ? 1 : 0;
my $ignoresize=$opts{i};

@ARGV >= 1 || die $syntax;

my $conffile=shift @ARGV;
die "Cannot find config file $conffile\n" if ! -e $conffile;

my %argconfig=();
foreach my $arg (@ARGV)
{
    die "Invalid configuration argument $arg: should be xxx=yyy"
        if $arg !~ /^(\w+)\=(.*)/;
    $argconfig{lc($1)} = $2;
}

my $confdir=$conffile;
$confdir =~ s/[^\\\/]*$//;
$confdir =~ s/[\\\/]$//;
$confdir = '.' if $confdir eq '';

my %config=Config::General->new(
    -ConfigFile=>$conffile,
    -LowerCaseNames=>1,
    -UseApacheInclude=>1,
    -IncludeRelative=>1 )->getall();

foreach my $k (keys %argconfig)
{
    $config{$k}=$argconfig{$k};
}

# Expand references in configuration

my %unresolved=();
foreach my $k (keys(%config))
{
    my $v=$config{$k};
    my $maxiterations=10;
    while( $maxiterations > 0 )
    {
        my $v0=$v;
        $v =~ s/(\{(\w+)\})/exists $config{lc($2)} ? $config{lc($2)} : $1/eg;
        $v =~ s/(\{ENV\:(\w+)\})/exists $ENV{$2} ? $ENV{$2} : $1/eg;
        $v =~ s/(\{(\w+)\})/! exists $patterns{$2} && exists $ENV{$2} ? $ENV{$2} : $1/eg;
        last if $v eq $v0;
    }
    foreach my $var ($v =~ /\{((?:ENV\:)\w+)\}/g)
    {
        $unresolved{$var}=1 if ! exists $patterns{$var};
    }
    $config{$k}=$v;
}
if( %unresolved )
{
    my $badvar=join(' ',sort keys %unresolved);
    die "The following configuration items are undefined: $badvar\n";
}

my $startage=$opts{s} // $config{startage};
my $endage=$opts{e} // $config{endage};

$config{maxruntime} = ($opts{r}+0) if $opts{r};
$config{pause} = $opts{p} if exists $opts{p};
$config{verbose} = 1 if $verbose;

my $codes=$config{codes};
$codes = join(' ',@$codes) if ref($codes) eq 'ARRAY';
$codes = join(' ',keys %$codes) if ref($codes) eq 'HASH';
my $validcodes={};
my $allcodes=0;
foreach my $c (split(' ',$codes))
{
    $allcodes=1 if $c eq '*';
    $validcodes->{uc($c)} = 1;
}
$allcodes = 1 if ! %$validcodes;

my $remotedir=$config{remotedir};
my $remotefilere=$config{remotefilere};
my $targetpath=$config{targetpath};
my $markerpath=$config{markerpath};
my $postdownloadcommand=$config{postdownloadcommand};
my $dochdir=$config{postdownloadcommandchdir};
$dochdir=0 if $dochdir =~ /(no|false)/i;

my $startdate=$startage =~ /^\d+$/ ? "now-$startage" : $startage;
$startdate=parse_gnss_date($startdate);
my $enddate=$endage =~ /^\d+$/ ? "now-$endage" : $endage;
$enddate=parse_gnss_date($enddate);

my $maxruntime=$config{maxruntime}+0;
my $jobexpirytime = $maxruntime > 0 ? time()+$maxruntime*60 : 0;

my $ftp=new FtpConnection( %config );

if( $verbose )
{
    print "------------------------------------------\n";
    print  "Mirror GNSS data       ",strftime("%Y:%m:%d %H:%M:%S",localtime),"\n"; 
    print  "Configuration:         $conffile\n";
    printf "Remote host:           %s\n",$ftp->host;
    printf "Remote user:           %s\n",$ftp->user;
    printf "Remote base dir:       %s\n",$ftp->basedir;
    print  "Remote directory:      $remotedir\n";
    print  "Remote file RE:        $remotefilere\n";
    print  "Target file path:      $targetpath\n";
    print  "Marker file path:      $markerpath\n";
    print  "Post download command: $postdownloadcommand\n";
    printf "Change dir for pdc   : %s\n",$dochdir ? 'yes' : 'no';
    printf "Start date:            %04d:%03d\n",seconds_yearday($startdate);
    printf "End date:              %04d:%03d\n",seconds_yearday($enddate);
    if( $maxruntime > 0 )
    {
        printf "Max run time:     %.1f minutes\n",$maxruntime;
    }
    if( $allcodes )
    {
        print "Downloading:      all codes\n";
    }
    else
    {
        printf "Downloading:      %d codes\n",scalar(keys %$validcodes);
    }
    printf "FTP timeout:     %s\n",$ftp->timeout;
}

# Ensure outputs are flushed to make it easy to monitor progress..
select(STDERR);
$|=1;
select(STDOUT);
$|=1;

$targetpath =~ s/^\~/$confdir/;
$markerpath =~ s/^\~/$confdir/;
$postdownloadcommand =~ s/(^|\s)\~\//$1.$confdir.'\/'/eg;

# @valid_dates is a list of possible date options that can be matched against fields directory/filenames.
# This is the starting point for filtering out the candidate files.

my @valid_dates=();
for( my $i = $startdate; $i <= $enddate; $i += $SECS_PER_DAY )
{
    my($yy,$mm,$dd)=(seconds_ymdhms($i));
    my($yy2,$skip,$ddd)=(time_elements($i));
    push(@valid_dates,{
            yyyy=>sprintf("%04d",$yy),
            yy=>sprintf("%02d",$yy%100),
            mm=>sprintf("%02d",$mm),
            ddd=>sprintf("%03d",$ddd),
            dd=>sprintf("%02d",$dd),
            mmm=>$months[$mm-1],
            MMM=>$umonths[$mm-1],
            Mmm=>$ccmonths[$mm-1],
            mmmm=>$fullmonths[$mm-1],
            MMMM=>$ufmonths[$mm-1],
            Mmmm=>$cfmonths[$mm-1],
        });
}

# @dirparts is an array of components of the remote file path.  Each in turn
# is used to filter down the valid options before passing on to the next directory.

my @dirparts;
foreach my $p ( split(/\//,$remotedir),$remotefilere)
{
    next if $p eq '';
    $p =~ s/\./\\./g;
    $p =~ s/\?/./g;
    $p =~ s/\*/.*/g;
    $p =~ s/\(\.\<(\w+)\>/(?<\1>/g;
    $p =~ s/(\{(\w+)\})/
             exists $patterns{$2} ?
                '(?<'.$2.'>'.$patterns{$2}.')' :
                $1
                /exg;
    $p = '^'.$p.'$';

    my @fields=( $p =~ /\(\?\<(\w+)\>/g );
    push( @dirparts, { re=>qr/$p/, fields=>\@fields });
}


downloadDir( $ftp, $ftp->basedir, \@valid_dates, \@dirparts );
$ftp->quit();

if( $verbose )
{
    print "\nFinished ",strftime("%Y:%m:%d %H:%M:%S",localtime),"\n"; 
    print "------------------------------------------\n";
}

#  filteredOptions:
#
#  $options is an array of currently valid options.  Each is a hash with a set of keys and values that match
#  $dirname is the name to filter the list against
#  $dirpart is the defines the structure of $dirname as a regular expression with named capture groups and a list of 
#     capture group names (fields)
#
#  Each currently valid option is tested against the name.  If all the fields in the name match currently existing
#  value in the option then it passes. Options that pass have any other fields in the name added to them.
#  
#  The field "code" is treated specially and is additionally matched against valid codes

sub filteredOptions
{
    my($options,$dirname,$dirpart) = @_;
    my @options=();
    my $fields=$dirpart->{fields};
    my $re=$dirpart->{re};
    my %fieldvalues=();
    return [] if $dirname !~ /$re/;
    my $code;
    foreach my $f (@$fields)
    {
        $fieldvalues{$f}=$+{$f};
        if(lc($f) eq 'code')
        {
            $fieldvalues{CODE}=uc($fieldvalues{$f});
            $fieldvalues{code}=lc($fieldvalues{$f});
            $fieldvalues{Code}=$fieldvalues{$f};
            $code=$fieldvalues{CODE};
        }
    }

    return [] if $code ne '' && ! $allcodes && ! $validcodes->{$code};

    my @matches=();

    foreach my $opt (@$options)
    {
        my $ok=1;
        foreach my $k (@$fields)
        {
            next if (! exists $opt->{$k}) || ($opt->{$k} eq $fieldvalues{$k});
            $ok = 0;
            last;
        }
        next if ! $ok;
        my %match=%$opt;
        while ( my ($k,$v) = each %fieldvalues ) { $match{$k}=$v; }
        push( @matches, \%match );
    }
    return \@matches;
}

sub expandTarget
{
    my ($opts,$file,$template,$variable,$location)=@_;
    my $result='';
    foreach my $opt (@$opts)
    {
        $opt->{filename} = $file;
        my $value=$template;
        $value =~ s/\{(\w+)\}/$opt->{$1}/eg;
        if( $value =~ /\{\w+\}/ )
        {
            print "** Unresolved $variable name $value for $location\n";
            return;
        }
        if( $result && $value ne $result )
        {
            print "** Ambiguous $variable for $location ($result,$value)\n";
            return;
        }
        $result = $value;
    }
return $result;
}

sub downloadDir
{
    my( $ftp, $dirname, $options, $dirparts ) = @_;
    return if $jobexpirytime && time > $jobexpirytime;
    print "Processing $dirname\n" if $verbose;
    if( ! $ftp->cwd($dirname) )
    {
        print "** Cannot access remote directory $dirname\n";
        return;
    }
    my ($dirs, $files) = $ftp->dirList();
    my @parts=@$dirparts;
    my $dirpart=shift(@parts);

    # If we are not at the list part of $dirparts, then we are matching against directories.
    # Recursively call this routine for valid directories.

    my $expired=0;
    if( @parts )
    {
        return if ! ref $dirs;
        foreach my $dir (@$dirs)
        {
            $expired=1;
            last if $jobexpirytime && time > $jobexpirytime;
            $expired=0;
            my $opts=filteredOptions($options,$dir,$dirpart);
            next if ! @$opts;
            downloadDir( $ftp, "$dirname/$dir", $opts, \@parts );
        }
        print "** Job terminated - maximum run time expired\n" if $expired;
        return;
    }

    # Otherwise we are matching against files for downloading, so 
    # try each one in turn

    return if ! ref $files;
    foreach my $file (sort keys %$files)
    {
        $expired=1;
        last if $jobexpirytime && time > $jobexpirytime;
        $expired=0;
        # Does it match the current filtered options
        my $opts=filteredOptions($options,$file,$dirpart);
        next if ! @$opts;

        # Build the target name. There may be more than one filtered option
        # remaining, so make sure that if so they uniquely define a target name.
        # If not then fail the download.

        my $target=expandTarget($opts,$file,$targetpath,'target file name',"$dirname/$file");
        my $marker=expandTarget($opts,$file,$markerpath,'marker file name',"$dirname/$file");
        my $command=expandTarget($opts,$file,$postdownloadcommand,'marker file name',"$dirname/$file");

        next if ! $target;


        # Now try and download the file
        if( -e $target )
        {
            if( ! -f $target )
            {
                print "** Cannot create file at $target - something is already there\n";
                next;
            }
            # Check the size - if it matches then assume the file is up to date.
            if( (! $ignoresize) && ($files->{$file}->{size} == -s $target) )
            {
                print "$target is already available and of the correct size\n" if $verbose > 1;
                next;
            }
        }
        if( ! $ignoresize && $marker && -f $marker )
        {
            my $size=0;
            if( open(my $mf, "<$marker") )
            {
                $size=<$mf>+0;
                close($mf);
            }
            if( $files->{$file}->{size} == $size )
            {
                print "$target marker file present and shows correct size\n" if $verbose > 1;
                next;
            }
        }

        # Ensure the target directory exists.. 
        my $tgtpath=$target;
        my $tgtname=$target;
        $tgtpath =~ s/[^\\\/]*$//;
        $tgtpath =~ s/[\\\/]$//;
        $tgtname =~ s/.*[\\\/]//;

        if( ! -d $tgtpath && ! File::Path::make_path($tgtpath) )
        {
            print "** Cannot create target directory at $tgtpath\n";
            next;
        }

        # Download to a temporary file first to ensure failed 
        # downloads don't generate incomplete files.

        my $tmp=$tgtpath.'/.download.'.$tgtname.'.tmp';
        unlink($tmp);

        if( ! $ftp->get($file,$tmp) )
        {
            print "** Failed to download $dirname/$file\n";
            print $ftp->message,"\n";
            unlink($tmp);
        }
        elsif( ! move($tmp,$target) )
        {
            print "Failed to overwrite $target\n";
            unlink($tmp);
        }
        else
        {
            print "Successfully downloaded $target\n" if $verbose;
            if( $command )
            {
                print "Running $command\n" if $verbose;
                my $cwd=getcwd;
                chdir($tgtpath) if $dochdir;
                foreach my $cmdline (split(/\n/,$command) )
                {
                    my @command=split(' ',$cmdline);
                    next if ! @command;
                    next if $command =~ /^\s*\#/;
                    eval
                    {
                        system(@command);
                    };
                    if( $@ )
                    {
                        print $@;
                    }
                }
                chdir($cwd);
            }
            if( $marker )
            {
                open(my $mf, ">$marker" );
                print $mf $files->{$file}->{size};
                close($mf);
            }
        }
    }
    print "** Job terminated - maximum run time expired\n" if $expired;
}

# Simplistic parsing of a directory listing.
#
# Assumes that directory and file names do not include space characters!
#
# Returns a list of direcories, and a hash of files keyed on the filename and
# having value the same as the directory entry

sub printExampleConfig
{
    my $started = 0;
    while( my $l = <DATA> )
    {
        $started=1 if $l =~ /^\s*\#/;
        next if ! $started;
        $l =~ s/^\s+//;
        $l = "\n" if $l eq '';
        print $l;
    }
}

#========================================================================

package FtpConnection;

sub new
{
    my ($class, %config) = @_;

    my $self={};
    $self->{verbose}=$config{verbose};
    $self->{remoteuri}=$config{remoteuri};
    $self->{user}=$config{remoteuser};
    $self->{password}=$config{remotepassword};
    $self->{timeout}=$config{timeout}+0 || 30;
    $self->{pause}=$config{downloadwait}+0 || 1; 
    $self->{reconnectwait}=$config{reconnectwait}+0 || 5;
    $self->{maxreconnect}=$config{maxreconnectiontries}+0 || 5;
    $self->{maxdownloads}=$config{maxdownloadsperconnection}+0 || 5;

    my $remoteuri=$self->{remoteuri};
    my $uri=URI->new($remoteuri);
    die "$remoteuri is not a valid FTP URI.\nMust be an ftp:// URI" if $uri->scheme != 'ftp';

    my $host=$uri->host;
    my $basedir=$uri->path;
    my ($uri_user,$uri_pwd) = split(/\:/,$uri->userinfo,2);

    $self->{host}=$host;
    $self->{user}=$uri_user if $uri_user;
    $self->{password}=$uri_pwd if $uri_pwd;
    $self->{basedir}=$basedir;

    $self->{_ftp} = undef;
    $self->{_dir} = $basedir;
    $self->{_ndownload} = 0;

    return $self=bless $self, $class;
}

sub verbose { return $_[0]->{verbose}; }
sub remoteuri { return $_[0]->{remoteuri}; }
sub host { return $_[0]->{host}; }
sub user { return $_[0]->{user}; }
sub password { return $_[0]->{password}; }
sub timeout { return $_[0]->{timeout}; }
sub pause { return $_[0]->{pause}; }
sub reconnectwait { return $_[0]->{reconnectwait}; }
sub maxreconnect { return $_[0]->{maxreconnect}; }
sub maxdownloads { return $_[0]->{maxdownloads}; }
sub basedir { return $_[0]->{basedir}; }

sub ftp
{
    my( $self, $force ) = @_;
    return $self->{_ftp} if $self->{_ftp} && ! $force;
    $self->quit;
    my $trying=$self->maxreconnect;
    $trying=1 if $trying < 1;
    my $host=$self->host;
    my $ftp;
    while( $trying-- )
    {
        print "Connecting to $host\n" if $self->verbose;
        $ftp=Net::FTP->new($host, Timeout=>$self->timeout);
        last if $ftp;
        sleep( $self->reconnectwait) if $trying;
    }

    if( ! $ftp )
    {
        die "Cannot make FTP connection to $host\n";
    }

    my $user=$self->user;
    my $password=$self->password;
    my $basedir=$self->basedir;

    $ftp->login($user,$password) || die "Cannot login to $host as $user to $host\n";
    $ftp->binary();
    $ftp->cwd($self->{_dir}) || die "Cannot cd to $basedir on $host\n";
    $self->{_ftp} = $ftp;
    return $ftp;
}

sub quit
{
    my( $self)=@_;
    $self->{_ftp}->quit() if $self->{_ftp};
    $self->{_ftp} = undef;
}

sub reconnect
{
    my( $self)=@_;
    $self->quit;
    sleep( $self->reconnectwait );
}

sub run
{
    my( $self, $sub )=@_;
}

sub cwd
{
    my( $self, $dir) = @_;
    $self->{_dir} = $dir;
    return $self->ftp->cwd($dir);
}

sub dirList
{
    my ($self) = @_;
    return $self->parseDir( $self->ftp->dir() );
}

sub size
{
    my ($self,$file) = @_;
    return $self->ftp->size($file);
}

sub get
{
    my ($self,$file,$target) = @_;
    if( $self->maxdownloads > 0 && $self->{_ndownload} >= $self->maxdownloads )
    {
        $self->reconnect;
        $self->{_ndownload} = 0;
    }
    sleep( $self->pause ) if $self->{_ndownload};
    my $ntries=0;
    my $success=0;
    while( 1 )
    {

        $success=$self->ftp->get($file,$target);
        last if $success;
        last if $ntries++ >= $self->maxreconnect;
        print "** Download of $file failed - reconnecting\n" if $self->verbose;
        $self->reconnect;
    }
    if( $success )
    {
        $self->{_ndownload}++;
        my $mtime=$self->ftp->mdtm( $file );
        utime $mtime,$mtime,$target if $mtime;
    }
    else
    {
        print "** Download of $file failed\n".$self->ftp->message."\n" 
            if $self->verbose && ! $success;
    }
    return $success;
}

sub parseDir
{
    my($self,@listing)=@_;
    my $dirs=[];
    my $files={};
    @listing=@{$listing[0]} if ref $listing[0];
    foreach my $l (@listing)
    {
        $l =~ s/^\s+//;
        $l =~ s/\s+$//;
        # <DIR> is Microsoft server test
        my $isdir=$l=~/^d/ || $l =~ /\<DIR\>/;
        my $name=(split(' ',$l))[-1];
        # Skip names starting with '.";
        next if $name eq '';
        next if $name =~ /^\./;
        if( $isdir )
        {
            push(@$dirs,$name);
        }
        else
        {
            # Select column based on unix or MS format listing
            my $sizecol =  $l =~ /^\d\d/ ? -2 : 4;
            my $size=(split(' ',$l))[$sizecol];
            $files->{$name}={size=>$size, dir=>$l };
        }
    }
    return $dirs,$files;
}

#========================================================================
__END__

=head2 Example configuration file
 
 # Example configuration file:
 #
 # Note all configuration item values can contain references to other configuration
 # item values as {xxxxx}, where xxx is the other item.  But note special values
 # for date components and file names that should not be used.
 #
 # {xxxx} strings that are not matched can be replaced by environment variables if
 # they exist, except for the code and time patterns listed below, which are 
 # replaced with components of the file path.
 #
 # {ENV:xxx} can be used to explicitly replace environment variables.
 #
 # Configuration items can be included from another file using:
 #
 # include filename
 #
 # Filenames are relative to this configuration file.
 
 # RemoteUri is the the base of the remote directory
 
 RemoteUri=ftp://ftp.geonet.org.nz/rawgps
 
 RemoteUser=anonymous
 
 RemotePassword=positionz@linz.govt.nz
 
 TimeOut 30
 
 # Delay in seconds added after each successful download to be polite to server
 
 DownloadWait 1

 # If a download fails the script will try to reconnect to the server
 # The following options control the reconnection 
 
 MaxReconnectionTries 5

 # Time delay in seconds between reconnection attempts

 ReconnectionWait 5

 # Auto reconnect after specified number of downloads
 
 MaxDownloadsPerConnection 100

 # Maximum runtime in minutes - once this time has expired the job will finish
 
 MaxRunTime 120
 
 # RemotePath is the path to the files to download.
 #
 # Can include {yyyy},{yy},{mmmm}, {mmm},{mm},{ddd},{dd} which will map to 
 # the corresponding date strings (mmm is 3 letter month name, mmmm is the 
 # full month name). Will also accept Mmm, MMM, Mmmm, MMMM for different 
 # capitalisation.

 # Also can accept {code} or {CODE} for upper or lower case four character
 # codes which will match a valid code. Use {Code} for a case insensitive
 # code match.
 # 
 # These will be replaced with values corresponding to the maximum number
 # of days before the current date to process.  Can also include ? for any
 # character, and * for any set of characters.
 
 RemoteDir=/{yyyy}/{ddd}
 
 # RemoteFileRe.  Remote file names are matched against this regular expression.
 # Files that match are candidates for downloading.  
 # This can include any of the time and code components, eg {ddd}. Also it
 # can include regular expression # capture groups (?<xxx>...) to capture 
 # fields that can be used in the target path.  
 
 RemoteFileRe={code}{yyyy}\d{8}[a-z].T02
 
 # Target directory.  This can be based absolute, or relative to the location
 # of the configuration file (defined as ~).  Can include time components
 # as for RemovePath.  Can also include {filename} to use the source filename.
 
 TargetPath=~/{yyyy}/{ddd}/{filename}

 # Marker file.  By default the script checks for the existence of the target 
 # file.  If it exists and is of the correct size it is downloaded again.  The 
 # script can instead use a marker file into which the downloaded size is written.
 # This allows the downloaded file to be removed or otherwise changed without 
 # being reprocessed.
 
 MarkerPath=~/{yyyy}/{ddd}/{filename}.downloaded

 # Processing script. Command to run once a file has been downloaded.  The script
 # is run in the target directory.  The command can include parameters, and can
 # include the replacement strings for filename, code, etc.
 #
 # If PostDownloadCommandChDir evaluates to true then the script changes to the 
 # download directory to execute the command.
 
 PostDownloadCommand process_downloaded_file.sh {filename}
 PostDownloadCommandChDir 1
 
 # Codes to download.  Use Codes * (or omit codes altogether), to download
 # all available codes.
 
 Codes KAIK RGRE
 Codes SCTB
 
 # Number of days before current date to start and end download
 
 StartAge 30
 EndAge 0
